MODULE DIRK

CONTAINS

SUBROUTINE SDIRK4(N,FCN,X,Y,XEND,H, &
                  RTOL,ATOL,ITOL, &
                  JAC ,IJAC,MLJAC,MUJAC, &
                  MAS ,IMAS,MLMAS,MUMAS, &
                  SOLOUT,IOUT, &
                  WORK,LWORK,IWORK,LIWORK,LRCONT,IDID)
!----------------------------------------------------------
!NUMERICAL SOLUTION OF A STIFF
!SYSTEM OF FIRST 0RDER ORDINARY DIFFERENTIAL EQUATIONS  BY'=F(X,Y).
!TH METHOD USED IS A SINGLY DIAGONALLY IMPLICIT RUNGE-KUTTA METHOD
!OF ORDER 4 (WITH STEP SIZE CONTROL).
!C.F. SECTION IV.6
!
!AUTHORS: E. HAIRER AND G. WANNER
!UNIVERSITE DE GENEVE, DEPT. DE MATHEMATIQUES
!CH-1211 GENEVE 24, SWITZERLAND
!E-MAIL:  HAIRER@CGEUGE51.BITNET,  WANNER@CGEUGE51.BITNET
!
!THIS CODE IS PART OF THE BOOK:
!E. HAIRER AND G. WANNER, SOLVING ORDINARY DIFFERENTIAL
!EQUATIONS II. STIFF AND DIFFERENTIAL-ALGEBRAIC PROBLEMS.
!SPRINGER SERIES IN COMPUTATIONAL MATHEMATICS,
!SPRINGER-VERLAG (1990)
!
!VERSION OF JUNE 22, 1995
!
!INPUT PARAMETERS
!----------------
!N           DIMENSION OF THE SYSTEM
!
!FCN         NAME (EXTERNAL) OF SUBROUTINE COMPUTING THE
!VALUE OF F(X,Y):
!SUBROUTINE FCN(N,X,Y,F)
!REAL*8 X,Y(N),F(N)
!F(1)=...   ETC.
!
!X           INITIAL X-VALUE
!
!Y(N)        INITIAL VALUES FOR Y
!
!XEND        FINAL X-VALUE (XEND-X MAY BE POSITIVE OR NEGATIVE)
!
!H           INITIAL STEP SIZE GUESS;
!FOR STIFF EQUATIONS WITH INITIAL TRANSIENT,
!H=1.D0/(NORM OF F'), USUALLY 1.D-2 OR 1.D-3, IS GOOD.
!THIS CHOICE IS NOT VERY IMPORTANT, THE CODE QUICKLY
!ADAPTS ITS STEP SIZE. STUDY THE CHOSEN VALUES FOR A FEW
!STEPS IN SUBROUTINE "SOLOUT", WHEN YOU ARE NOT SURE.
!(IF H=0.D0, THE CODE PUTS H=1.D-6).
!
!RTOL,ATOL   RELATIVE AND ABSOLUTE ERROR TOLERANCES. THEY
!CAN BE BOTH SCALARS OR ELSE BOTH VECTORS OF LENGTH N.
!
!ITOL        SWITCH FOR RTOL AND ATOL:
!ITOL=0: BOTH RTOL AND ATOL ARE SCALARS.
!THE CODE KEEPS, ROUGHLY, THE LOCAL ERROR OF
!Y(I) BELOW RTOL*ABS(Y(I))+ATOL
!ITOL=1: BOTH RTOL AND ATOL ARE VECTORS.
!THE CODE KEEPS THE LOCAL ERROR OF Y(I) BELOW
!RTOL(I)*ABS(Y(I))+ATOL(I).
!
!JAC         NAME (EXTERNAL) OF THE SUBROUTINE WHICH COMPUTES
!THE PARTIAL DERIVATIVES OF F(X,Y) WITH RESPECT TO Y
!(THIS ROUTINE IS ONLY CALLED IF IJAC=1; SUPPLY
!A DUMMY SUBROUTINE IN THE CASE IJAC=0).
!FOR IJAC=1, THIS SUBROUTINE MUST HAVE THE FORM
!SUBROUTINE JAC(N,X,Y,DFY,LDFY)
!REAL*8 X,Y(N),DFY(LDFY,N)
!DFY(1,1)= ...
!LDFY, THE COLOMN-LENGTH OF THE ARRAY, IS
!FURNISHED BY THE CALLING PROGRAM.
!IF (MLJAC == N) THE JACOBIAN IS SUPPOSED TO
!BE FULL AND THE PARTIAL DERIVATIVES ARE
!STORED IN DFY AS
!DFY(I,J) = PARTIAL F(I) / PARTIAL Y(J)
!ELSE, THE JACOBIAN IS TAKEN AS BANDED AND
!THE PARTIAL DERIVATIVES ARE STORED
!DIAGONAL-WISE AS
!DFY(I-J+MUJAC+1,J) = PARTIAL F(I) / PARTIAL Y(J).
!
!IJAC        SWITCH FOR THE COMPUTATION OF THE JACOBIAN:
!IJAC=0: JACOBIAN IS COMPUTED INTERNALLY BY FINITE
!DIFFERENCES, SUBROUTINE "JAC" IS NEVER CALLED.
!IJAC=1: JACOBIAN IS SUPPLIED BY SUBROUTINE JAC.
!
!MLJAC       SWITCH FOR THE BANDED STRUCTURE OF THE JACOBIAN:
!MLJAC=N: JACOBIAN IS A FULL MATRIX. THE LINEAR
!ALGEBRA IS DONE BY FULL-MATRIX GAUSS-ELIMINATION.
!0<=MLJAC<N: MLJAC IS THE LOWER BANDWITH OF JACOBIAN
!MATRIX (>= NUMBER OF NON-ZERO DIAGONALS BELOW
!THE MAIN DIAGONAL).
!
!MUJAC       UPPER BANDWITH OF JACOBIAN  MATRIX (>= NUMBER OF NON-
!ZERO DIAGONALS ABOVE THE MAIN DIAGONAL).
!NEED NOT BE DEFINED IF MLJAC=N.
!
!----   MAS,IMAS,MLMAS, AND MUMAS HAVE ANALOG MEANINGS      -----
!----   FOR THE "MASS MATRIX" (THE MATRIX "M" OF SECTION IV.8): -
!
!MAS         NAME (EXTERNAL) OF SUBROUTINE COMPUTING THE MASS-
!MATRIX M.
!IF IMAS=0, THIS MATRIX IS ASSUMED TO BE THE IDENTITY
!MATRIX AND NEEDS NOT TO BE DEFINED;
!SUPPLY A DUMMY SUBROUTINE IN THIS CASE.
!IF IMAS=1, THE SUBROUTINE MAS IS OF THE FORM
!SUBROUTINE MAS(N,AM,LMAS)
!REAL*8 AM(LMAS,N)
!AM(1,1)= ....
!IF (MLMAS == N) THE MASS-MATRIX IS STORED
!AS FULL MATRIX LIKE
!AM(I,J) = M(I,J)
!ELSE, THE MATRIX IS TAKEN AS BANDED AND STORED
!DIAGONAL-WISE AS
!AM(I-J+MUMAS+1,J) = M(I,J).
!
!IMAS       GIVES INFORMATION ON THE MASS-MATRIX:
!IMAS=0: M IS SUPPOSED TO BE THE IDENTITY
!MATRIX, MAS IS NEVER CALLED.
!IMAS=1: MASS-MATRIX  IS SUPPLIED.
!
!MLMAS       SWITCH FOR THE BANDED STRUCTURE OF THE MASS-MATRIX:
!MLMAS=N: THE FULL MATRIX CASE. THE LINEAR
!ALGEBRA IS DONE BY FULL-MATRIX GAUSS-ELIMINATION.
!0<=MLMAS<N: MLMAS IS THE LOWER BANDWITH OF THE
!MATRIX (>= NUMBER OF NON-ZERO DIAGONALS BELOW
!THE MAIN DIAGONAL).
!MLMAS IS SUPPOSED TO BE  <=  MLJAC.
!
!MUMAS       UPPER BANDWITH OF MASS-MATRIX (>= NUMBER OF NON-
!ZERO DIAGONALS ABOVE THE MAIN DIAGONAL).
!NEED NOT BE DEFINED IF MLMAS=N.
!MUMAS IS SUPPOSED TO BE  <=  MUJAC.
!
!SOLOUT      NAME (EXTERNAL) OF SUBROUTINE PROVIDING THE
!NUMERICAL SOLUTION DURING INTEGRATION.
!IF IOUT=1, IT IS CALLED AFTER EVERY SUCCESSFUL STEP.
!SUPPLY A DUMMY SUBROUTINE IF IOUT=0.
!IT MUST HAVE THE FORM
!SUBROUTINE SOLOUT (NR,XOLD,X,Y,N,IRTRN)
!REAL*8 X,Y(N)
!....
!SOLOUT FURNISHES THE SOLUTION "Y" AT THE NR-TH
!GRID-POINT "X" (THEREBY THE INITIAL VALUE IS
!THE FIRST GRID-POINT).
!"XOLD" IS THE PRECEEDING GRID-POINT.
!"IRTRN" SERVES TO INTERRUPT THE INTEGRATION. IF IRTRN
!IS SET <0, RADAU5 RETURNS TO THE CALLING PROGRAM.
!
!-----  CONTINUOUS OUTPUT: -----
!DURING CALLS TO "SOLOUT", A CONTINUOUS SOLUTION
!FOR THE INTERVAL [XOLD,X] IS AVAILABLE THROUGH
!THE REAL*8 FUNCTION
!>>>   CONTS4(I,S)   <<<
!WHICH PROVIDES AN APPROXIMATION TO THE I-TH
!COMPONENT OF THE SOLUTION AT THE POINT S. THE VALUE
!S SHOULD LIE IN THE INTERVAL [XOLD,X].
!
!IOUT        SWITCH FOR CALLING THE SUBROUTINE SOLOUT:
!IOUT=0: SUBROUTINE IS NEVER CALLED
!IOUT=1: SUBROUTINE IS AVAILABLE FOR OUTPUT.
!
!WORK        ARRAY OF WORKING SPACE OF LENGTH "LWORK".
!SERVES AS WORKING SPACE FOR ALL VECTORS AND MATRICES.
!"LWORK" MUST BE AT LEAST
!N*(LJAC+LMAS+LE1+12)+7
!WHERE
!LJAC=N              IF MLJAC=N (FULL JACOBIAN)
!LJAC=MLJAC+MUJAC+1  IF MLJAC<N (BANDED JAC.)
!AND
!LMAS=0              IF IMAS=0
!LMAS=N              IF IMAS=1 AND MLMAS=N (FULL)
!LMAS=MLMAS+MUMAS+1  IF MLMAS<N (BANDED MASS-M.)
!AND
!LE1=N               IF MLJAC=N (FULL JACOBIAN)
!LE1=2*MLJAC+MUJAC+1 IF MLJAC<N (BANDED JAC.).
!IN THE USUAL CASE WHERE THE JACOBIAN IS FULL AND THE
!MASS-MATRIX IS THE INDENTITY (IMAS=0), THE MINIMUM
!STORAGE REQUIREMENT IS
!LWORK = 2*N*N+12*N+7.
!
!LWORK       DECLARED LENGHT OF ARRAY "WORK".
!
!IWORK       INTEGER WORKING SPACE OF LENGHT "LIWORK".
!"LIWORK" MUST BE AT LEAST 2*N+4.
!
!LIWORK      DECLARED LENGHT OF ARRAY "IWORK".
!
!LRCONT      DECLARED LENGTH OF COMMON BLOCK
!>>>  COMMON /CONT/ICONT(4),RCONT(LRCONT)  <<<
!WHICH MUST BE DECLARED IN THE CALLING PROGRAM.
!"LRCONT" MUST BE AT LEAST
!5*N+2 .
!THIS IS USED FOR STORING THE COEFFICIENTS OF THE
!CONTINUOUS SOLUTION AND MAKES THE CALLING LIST FOR THE
!FUNCTION "CONTS4" AS SIMPLE AS POSSIBLE.
!
!----------------------------------------------------------------------
!
!SOPHISTICATED SETTING OF PARAMETERS
!-----------------------------------
!SEVERAL PARAMETERS OF THE CODE ARE TUNED TO MAKE IT WORK
!WELL. THEY MAY BE DEFINED BY SETTING WORK(1),..,WORK(7)
!AS WELL AS IWORK(1),..,IWORK(4) DIFFERENT FROM ZERO.
!FOR ZERO INPUT, THE CODE CHOOSES DEFAULT VALUES:
!
!IWORK(1)  IF IWORK(1) /= 0, THE CODE TRANSFORMS THE JACOBIAN
!MATRIX TO HESSENBERG FORM. THIS IS PARTICULARLY
!ADVANTAGEOUS FOR LARGE SYSTEMS WITH FULL JACOBIAN.
!IT DOES NOT WORK FOR BANDED JACOBIAN (MLJAC<N)
!AND NOT FOR IMPLICIT SYSTEMS (IMAS=1). IT IS
!ALSO NOT GOOD FOR SPARSE JACOBIANS.
!
!IWORK(2)  THIS IS THE MAXIMAL NUMBER OF ALLOWED STEPS.
!THE DEFAULT VALUE (FOR IWORK(2)=0) IS 100000.
!
!IWORK(3)  THE MAXIMUM NUMBER OF NEWTON ITERATIONS FOR THE
!SOLUTION OF THE IMPLICIT SYSTEM IN EACH STEP.
!THE DEFAULT VALUE (FOR IWORK(3)=0) IS 7.
!
!IWORK(4)  SWITCH FOR THE COEFFICIENTS OF THE METHOD
!IWORK(4)=1  COEFFICIENTS WITH GAMMA=0.25
!IWORK(4)=2  COEFFICIENTS WITH GAMMA=4./15.
!THE DEFAULT VALUE (FOR IWORK(4)=0) IS 2.
!
!WORK(1)   UROUND, THE ROUNDING UNIT, DEFAULT 1.D-16.
!
!WORK(2)   THE SAFETY FACTOR IN STEP SIZE PREDICTION,
!DEFAULT 0.9D0.
!
!WORK(3)   DECIDES WHETHER THE JACOBIAN SHOULD BE RECOMPUTED;
!INCREASE WORK(3), TO 0.1 SAY, WHEN JACOBIAN EVALUATIONS
!ARE COSTLY. FOR SMALL SYSTEMS WORK(3) SHOULD BE SMALLER
!(0.001D0, SAY).
!DEFAULT 0.001D0.
!
!WORK(4)   STOPPING CRIERION FOR NEWTON'S METHOD, USUALLY CHOSEN <1.
!SMALLER VALUES OF WORK(4) MAKE THE CODE SLOWER, BUT SAFER.
!DEFAULT 0.03D0.
!
!WORK(5) AND WORK(6) : IF WORK(5) < HNEW/HOLD < WORK(6), THEN THE
!STEP SIZE IS NOT CHANGED. THIS SAVES, TOGETHER WITH A
!LARGE WORK(3), LU-DECOMPOSITIONS AND COMPUTING TIME FOR
!LARGE SYSTEMS. FOR SMALL SYSTEMS ONE MAY HAVE
!WORK(5)=1.D0, WORK(6)=1.2D0, FOR LARGE FULL SYSTEMS
!WORK(5)=0.99D0, WORK(6)=2.D0 MIGHT BE GOOD.
!DEFAULTS WORK(5)=1.D0, WORK(6)=1.2D0 .
!
!WORK(7)   MAXIMAL STEP SIZE, DEFAULT XEND-X.
!
!-----------------------------------------------------------------------
!
!OUTPUT PARAMETERS
!-----------------
!X           X-VALUE FOR WHICH THE SOLUTION HAS BEEN COMPUTED
!(AFTER SUCCESSFUL RETURN X=XEND).
!
!Y(N)        NUMERICAL SOLUTION AT X
!
!H           PREDICTED STEP SIZE OF THE LAST ACCEPTED STEP
!
!IDID        REPORTS ON SUCCESSFULNESS UPON RETURN:
!IDID=1  COMPUTATION SUCCESSFUL,
!IDID=-1 COMPUTATION UNSUCCESSFUL.
!
!-----------------------------------------------------------------------
!*** *** *** *** *** *** *** *** *** *** *** *** ***
!DECLARATIONS
!*** *** *** *** *** *** *** *** *** *** *** *** ***
IMPLICIT REAL*8 (A-H,O-Z)
DIMENSION Y(N),ATOL(1),RTOL(1),WORK(LWORK),IWORK(LIWORK)
LOGICAL IMPLCT,JBAND,ARRET
EXTERNAL FCN,JAC,MAS,SOLOUT
COMMON/STAT/NFCN,NJAC,NSTEP,NACCPT,NREJCT,NDEC,NSOL
! --- COMMON STAT CAN BE INSPECTED FOR STATISTICAL PURPOSES:
! ---    NFCN      NUMBER OF FUNCTION EVALUATIONS (THOSE FOR NUMERICAL
!                  EVALUATION OF THE JACOBIAN ARE NOT COUNTED)  
! ---    NJAC      NUMBER OF JACOBIAN EVALUATIONS (EITHER ANALYTICALLY
!                  OR NUMERICALLY)
! ---    NSTEP     NUMBER OF COMPUTED STEPS
! ---    NACCPT    NUMBER OF ACCEPTED STEPS
! ---    NREJCT    NUMBER OF REJECTED STEPS (AFTER AT LEAST ONE STEP
!                  HAS BEEN ACCEPTED)
! ---    NDEC      NUMBER OF LU-DECOMPOSITIONS
! ---    NSOL      NUMBER OF FORWARD-BACKWARD SUBSTITUTIONS
! *** *** *** *** *** *** ***
!        SETTING THE PARAMETERS 
! *** *** *** *** *** *** ***
NFCN=0
NJAC=0
NSTEP=0
NACCPT=0
NREJCT=0
NDEC=0
NSOL=0
ARRET=.FALSE.
!-------- SWITCH FOR TRANSFORMATION OF JACOBIAN TO HESSIAN FORM ---
NHESS=IWORK(1)
IF (N <= 2) NHESS=0
!-------- NMAX , THE MAXIMAL NUMBER OF STEPS -----
IF (IWORK(2) == 0) THEN
   NMAX=100000
ELSE
   NMAX=IWORK(2)
   IF (NMAX <= 0) THEN
      WRITE(6,*)' WRONG INPUT IWORK(2)=',IWORK(2)
      ARRET=.TRUE.
   END IF
END IF
! -------- NIT    MAXIMAL NUMBER OF NEWTON ITERATIONS
IF (IWORK(3) == 0) THEN
   NIT=7
ELSE
   NIT=IWORK(3)
   IF (NIT <= 0) THEN
      WRITE(6,*)' CURIOUS INPUT IWORK(3)=',IWORK(3)
      ARRET=.TRUE.
   END IF
END IF
! -------- METH    SWITCH FOR THE COEFFICIENTS OF THE METHOD
IF (IWORK(4) == 0) THEN
   METH=2
ELSE
   METH=IWORK(4)
   IF (METH <= 0.OR.METH >= 3) THEN
      WRITE(6,*)' CURIOUS INPUT IWORK(4)=',IWORK(4)
      ARRET=.TRUE.
   END IF
END IF
!-------- UROUND   SMALLEST NUMBER SATISFYING 1.D0+UROUND>1.D0
IF (WORK(1) == 0.D0) THEN
   UROUND=1.D-16
ELSE
   UROUND=WORK(1)
   IF (UROUND <= 1.D-19.OR.UROUND >= 1.D0) THEN
      WRITE(6,*)' COEFFICIENTS HAVE 20 DIGITS, UROUND=',WORK(1)
      ARRET=.TRUE.
   END IF
END IF
! --------- SAFE     SAFETY FACTOR IN STEP SIZE PREDICTION
IF (WORK(2) == 0.D0) THEN
   SAFE=0.9D0
ELSE
   SAFE=WORK(2)
   IF (SAFE <= .001D0.OR.SAFE >= 1.D0) THEN
      WRITE(6,*)' CURIOUS INPUT FOR WORK(2)=',WORK(2)
      ARRET=.TRUE.
   END IF
END IF
!------ THET     DECIDES WHETHER THE JACOBIAN SHOULD BE RECOMPUTED;
IF (WORK(3) == 0.D0) THEN
   THET=0.001D0
ELSE
   THET=WORK(3)
END IF
!--- FNEWT   STOPPING CRIERION FOR NEWTON'S METHOD, USUALLY CHOSEN <1.
IF (WORK(4) == 0.D0) THEN
   FNEWT=0.03D0
ELSE
   FNEWT=WORK(4)
END IF
! --- QUOT1 AND QUOT2: IF QUOT1 < HNEW/HOLD < QUOT2, STEP SIZE = CONST.
IF (WORK(5) == 0.D0) THEN
   QUOT1=1.D0
ELSE
   QUOT1=WORK(5)
END IF
IF (WORK(6) == 0.D0) THEN
   QUOT2=1.2D0
ELSE
   QUOT2=WORK(6)
END IF
! -------- MAXIMAL STEP SIZE
IF (WORK(7) == 0.D0) THEN
   HMAX=XEND-X
ELSE
   HMAX=WORK(7)
END IF
! --------- CHECK IF TOLERANCES ARE O.K.
IF (ITOL == 0) THEN
   IF (ATOL(1) <= 0.D0.OR.RTOL(1) <= 10.D0*UROUND) THEN
      WRITE (6,*) ' TOLERANCES ARE TOO SMALL'
      ARRET=.TRUE.
   END IF
ELSE
   DO I=1,N
   IF (ATOL(I) <= 0.D0.OR.RTOL(I) <= 10.D0*UROUND) THEN
      WRITE (6,*) ' TOLERANCES(',I,') ARE TOO SMALL'
      ARRET=.TRUE.
   END IF
   ENDDO
END IF
!*** *** *** *** *** *** *** *** *** *** *** *** ***
!COMPUTATION OF ARRAY ENTRIES
!*** *** *** *** *** *** *** *** *** *** *** *** ***
!---- IMPLICIT, BANDED OR NOT ?
IMPLCT=IMAS /= 0
JBAND=MLJAC /= N
ARRET=.FALSE.
!-------- COMPUTATION OF THE ROW-DIMENSIONS OF THE 2-ARRAYS ---
! -- JACOBIAN 
IF(JBAND)THEN
   LDJAC=MLJAC+MUJAC+1
ELSE
   LDJAC=N
END IF
! -- MATRIX E FOR LINEAR ALGEBRA
IF(JBAND)THEN
   LDE=2*MLJAC+MUJAC+1
ELSE
   LDE=N
END IF
! -- MASS MATRIX
IF (IMPLCT) THEN
   IF (MLMAS /= N) THEN
      LDMAS=MLMAS+MUMAS+1
   ELSE
      LDMAS=N
   END IF
! ------ BANDWITH OF "MAS" NOT LARGER THAN BANDWITH OF "JAC"
   IF (MLMAS.GT.MLJAC.OR.MUMAS.GT.MUJAC) THEN
      WRITE (6,*) 'BANDWITH OF "MAS" NOT LARGER THAN BANDWITH OF "JAC"'
      ARRET=.TRUE.
   END IF
ELSE
   LDMAS=0
END IF
LDMAS2=MAX(1,LDMAS)
! ------ HESSENBERG OPTION ONLY FOR EXPLICIT EQU. WITH FULL JACOBIAN
IF ((IMPLCT.OR.JBAND).AND.NHESS /= 0) THEN
   WRITE(6,*)' HESSENBERG OPTION ONLY FOR EXPLICIT EQUATIONS WITH FULL JACOBIAN'
   ARRET=.TRUE.
END IF
!------- PREPARE THE ENTRY-POINTS FOR THE ARRAYS IN WORK -----
IEYHAT=8
IEZ=IEYHAT+N
IEY0=IEZ+N
IEZ1=IEY0+N
IEZ2=IEZ1+N
IEZ3=IEZ2+N
IEZ4=IEZ3+N
IEZ5=IEZ4+N
IESCAL=IEZ5+N
IEF1=IESCAL+N
IEG1=IEF1+N
IEH1=IEG1+N
IEJAC=IEH1+N
IEMAS=IEJAC+N*LDJAC
IEE=IEMAS+N*LDMAS
!------ TOTAL STORAGE REQUIREMENT -----------
ISTORE=IEE+N*LDE-1
IF (ISTORE > LWORK) THEN
   WRITE(6,*)' INSUFFICIENT STORAGE FOR WORK, MIN. LWORK=',ISTORE
   ARRET=.TRUE.
END IF
!------- ENTRY POINTS FOR INTEGER WORKSPACE -----
IEIP=5
IEHES=IEIP+N
!--------- TOTAL REQUIREMENT ---------------
ISTORE=IEHES+N-1
IF (ISTORE > LIWORK) THEN
   WRITE(6,*)' INSUFF. STORAGE FOR IWORK, MIN. LIWORK=',ISTORE
   ARRET=.TRUE.
END IF
!--------- CONTROL OF LENGTH OF COMMON BLOCK "CONT" -------
IF (LRCONT < (5*N+2)) THEN
   WRITE(6,*)' INSUFF. STORAGE FOR RCONT, MIN. LRCONT=',5*N+2
   ARRET=.TRUE.
   END IF
! ------ WHEN A FAIL HAS OCCURED, WE RETURN WITH IDID=-1
IF (ARRET) THEN
   IDID=-1
   RETURN
END IF
!-------- CALL TO CORE INTEGRATOR ------------
CALL SDICOR(N,FCN,X,Y,XEND,HMAX,H,RTOL,ATOL,ITOL,&
     JAC,IJAC,MLJAC,MUJAC,MAS,MLMAS,MUMAS,SOLOUT,IOUT,IDID,&
     NMAX,UROUND,SAFE,THET,FNEWT,QUOT1,QUOT2,NIT,METH,NHESS,&
     IMPLCT,JBAND,LDJAC,LDE,LDMAS2,&
     WORK(IEYHAT),WORK(IEZ),WORK(IEY0),WORK(IEZ1),WORK(IEZ2),&
     WORK(IEZ3),WORK(IEZ4),WORK(IEZ5),WORK(IESCAL),WORK(IEF1),&
     WORK(IEG1),WORK(IEH1),WORK(IEJAC),WORK(IEE),&
     WORK(IEMAS),IWORK(IEIP),IWORK(IEHES))
!----------- RETURN -----------
RETURN
END
!
!
!
!----- ... AND HERE IS THE CORE INTEGRATOR  ----------
!
SUBROUTINE SDICOR(N,FCN,X,Y,XEND,HMAX,H,RTOL,ATOL,ITOL,&
           JAC,IJAC,MLJAC,MUJAC,MAS,MLMAS,MUMAS,SOLOUT,IOUT,IDID,&
           NMAX,UROUND,SAFE,THET,FNEWT,QUOT1,QUOT2,NIT,METH,NHESS,&
           IMPLCT,BANDED,LDJAC,LE,LDMAS,&
           YHAT,Z,Y0,Z1,Z2,Z3,Z4,Z5,SCAL,F1,G1,H1,FJAC,E,FMAS,IP,IPHES)
!----------------------------------------------------------
!CORE INTEGRATOR FOR SDIRK4
!PARAMETERS SAME AS IN SDIRK4 WITH WORKSPACE ADDED
!----------------------------------------------------------
!DECLARATIONS
!----------------------------------------------------------
IMPLICIT REAL*8 (A-H,O-Z)
REAL*8 Y(N),YHAT(N),Z(N),Y0(N),Z1(N),Z2(N),Z3(N),Z4(N),Z5(N)
REAL*8 SCAL(N),F1(N),G1(N),H1(N)
REAL*8 FJAC(LDJAC,N),E(LE,N),FMAS(LDMAS,N)
REAL*8 ATOL(1),RTOL(1)
INTEGER IP(N),IPHES(N)
LOGICAL REJECT,FIRST,IMPLCT,BANDED,CALJAC,NEWTRE
COMMON /CONT/NN,NN2,NN3,NN4,XOLD,HSOL,CONT(1)
COMMON/STAT/NFCN,NJAC,NSTEP,NACCPT,NREJCT,NDEC,NSOL
!*** *** *** *** *** *** ***
!INITIALISATIONS
!*** *** *** *** *** *** ***
!--------- DUPLIFY N FOR COMMON BLOCK CONT -----
NN=N
NN2=2*N
NN3=3*N
NN4=4*N
!------- COMPUTE MASS MATRIX FOR IMPLICIT CASE ----------
IF (IMPLCT) CALL MAS(N,FMAS,LDMAS)
!---------- CONSTANTS ---------
MBDIAG=MUMAS+1
IF (BANDED) THEN
   MLE=MLJAC
   MUE=MUJAC
   MBJAC=MLJAC+MUJAC+1
   MBB=MLMAS+MUMAS+1
   MDIAG=MLE+MUE+1
   MDIFF=MLE+MUE-MUMAS
END IF
IF (METH == 1) THEN
   !-------- METHOD WITH GAMMA =1/4 ----------
   GAMMA=0.25D0
   C2=0.75D0
   C3=11.0D0/20.0D0
   C4=0.5D0
   ALPH21=2.0D0
   ALPH31=42.0D0/25.0D0
   ALPH32=-4.0D0/25.0D0
   ALPH41=89.0D0/68.0D0
   ALPH42=-25.0D0/136.0D0
   ALPH43=15.0D0/136.0D0
   ALPH51=-37.0D0/12.0D0
   ALPH52=-103.0D0/24.0D0
   ALPH53=275.0D0/8.0D0
   ALPH54=-85.0D0/3.0D0
   E1=-23.0D0/6.0D0
   E2=-17.0D0/12.0D0
   E3=125.0D0/4.0D0
   E4=-85.0D0/3.0D0
   D11=61.D0/27.D0
   D12=-185.D0/54.D0
   D13=2525.D0/18.D0
   D14=-3740.D0/27.D0
   D15=-44.D0/9.D0
   D21=2315.D0/81.D0
   D22=1049.D0/162.D0
   D23=-27725.D0/54.D0
   D24=40460.D0/81.D0
   D25=557.D0/27.D0
   D31=-6178.D0/81.D0
   D32=-1607.D0/81.D0
   D33=20075.D0/27.D0
   D34=-56440.D0/81.D0
   D35=-718.D0/27.D0
   D41=3680.D0/81.D0
   D42=1360.D0/81.D0
   D43=-10000.D0/27.D0
   D44=27200.D0/81.D0
   D45=320.D0/27.D0
   ETA1= 3.D0
   ANU1= 0.88D0
   ANU2= 0.44D0
   AMU1= 3.D0/17.D0
   AMU3= 155.D0/187.D0
END IF
IF (METH == 2) THEN
   !---------- METHOD WITH GAMMA = 4/15 ---------------
   GAMMA=4.0D0/15.0D0
   C2=23.0D0/30.0D0
   C3=17.0D0/30.0D0
   C4=2881.0D0/28965.0D0+GAMMA
   ALPH21=15.0D0/8.0D0
   ALPH31=1577061.0D0/922880.0D0
   ALPH32=-23427.0D0/115360.0D0
   ALPH41=647163682356923881.0D0/2414496535205978880.0D0
   ALPH42=-593512117011179.0D0/3245291041943520.0D0
   ALPH43=559907973726451.0D0/1886325418129671.0D0
   ALPH51=724545451.0D0/796538880.0D0
   ALPH52=-830832077.0D0/267298560.0D0
   ALPH53=30957577.0D0/2509272.0D0
   ALPH54=-69863904375173.0D0/6212571137048.0D0
   E1=7752107607.0D0/11393456128.0D0
   E2=-17881415427.0D0/11470078208.0D0
   E3=2433277665.0D0/179459416.0D0
   E4=-96203066666797.0D0/6212571137048.0D0
   D11= 24.74416644927758D0
   D12= -4.325375951824688D0
   D13= 41.39683763286316D0
   D14= -61.04144619901784D0
   D15= -3.391332232917013D0
   D21= -51.98245719616925D0
   D22= 10.52501981094525D0
   D23= -154.2067922191855D0
   D24= 214.3082125319825D0
   D25= 14.71166018088679D0
   D31= 33.14347947522142D0
   D32= -19.72986789558523D0
   D33= 230.4878502285804D0
   D34= -287.6629744338197D0
   D35= -18.99932366302254D0
   D41= -5.905188728329743D0
   D42= 13.53022403646467D0
   D43= -117.6778956422581D0
   D44= 134.3962081008550D0
   D45= 8.678995715052762D0
   ETA1=23.D0/8.D0
   ANU1= 0.9838473040915402D0
   ANU2= 0.3969226768377252D0
   AMU1= 0.6563374010466914D0
   AMU3= 0.3372498196189311D0
END IF
POSNEG=SIGN(1.D0,XEND-X)
HMAX1=MIN(ABS(HMAX),ABS(XEND-X))
IF (ABS(H) <= 10.D0*UROUND) H=1.0D-6
H=MIN(ABS(H),HMAX1) 
H=SIGN(H,POSNEG)
HOLD=H
CFAC=SAFE*(1+2*NIT)
NEWTRE=.FALSE.
REJECT=.FALSE.
FIRST=.TRUE.
FACCO1=1.D0
FACCO2=1.D0
FACCO3=1.D0
FACCO4=1.D0
FACCO5=1.D0
NSING=0
XOLD=X
IF (IOUT /= 0) THEN
   IRTRN=1
   NRSOL=1
   XOSOL=XOLD
   XSOL=X
   CONT(1:N) = Y(1:N)
   NSOLU=N
   HSOL=HOLD
   CALL SOLOUT(NRSOL,XOSOL,XSOL,CONT,NSOLU,IRTRN)
   IF (IRTRN < 0) GOTO 79
END IF
IF (ITOL == 0) THEN
   SCAL(1:N)=ATOL(1)+RTOL(1)*DABS(Y(1:N))
ELSE
   SCAL(1:N)=ATOL(1:N)+RTOL(1:N)*DABS(Y(1:N))
END IF
IF (IJAC == 0) CALL FCN(N,X,Y,Y0)
IF (IJAC == 0) NFCN=NFCN+1
! --- BASIC INTEGRATION STEP
10 CONTINUE
!*** *** *** *** *** *** ***
!COMPUTATION OF THE JACOBIAN
!*** *** *** *** *** *** ***
NJAC=NJAC+1
IF (IJAC.EQ.0) THEN
   ! --- COMPUTE JACOBIAN MATRIX NUMERICALLY
   IF (BANDED) THEN
      ! --- JACOBIAN IS BANDED
      MUJACP=MUJAC+1
      MD=MIN(MBJAC,N)
      DO K=1,MD
         J=K
         DO
            Z(J)=Y(J)
            G1(J)=DSQRT(UROUND*MAX(1.D-5,ABS(Y(J))))
            Y(J)=Y(J)+G1(J)
            J=J+MD
            IF (J > N) EXIT
         ENDDO
         CALL FCN(N,X,Y,CONT)
         J=K
         LBEG=MAX(1,J-MUJAC)
         DO
            LEND=MIN(N,J+MLJAC)
            Y(J)=Z(J)
            MUJACJ=MUJACP-J
            DO L=LBEG,LEND
               FJAC(L+MUJACJ,J)=(CONT(L)-Y0(L))/G1(J)
            ENDDO
            J=J+MD
            LBEG=LEND+1
            IF (J > N) EXIT
         ENDDO
      ENDDO
   ELSE
      ! --- JACOBIAN IS FULL
      DO I=1,N
         YSAFE=Y(I)
         DELT=DSQRT(UROUND*MAX(1.D-5,ABS(YSAFE)))
         Y(I)=YSAFE+DELT
         CALL FCN(N,X,Y,CONT)
         FJVAC(1:N,I)= (CONT(1:N)-Y0(1:N))/DELT
         Y(I)=YSAFE
      ENDDO
   END IF
ELSE
! --- COMPUTE JACOBIAN MATRIX ANALYTICALLY
CALL JAC(N,X,Y,FJAC,LDJAC)
END IF
CALJAC=.TRUE.
IF (NHESS /= 0) CALL ELMHES (LDJAC,N,1,N,FJAC,IPHES)
20 CONTINUE
!*** *** *** *** *** *** ***
!COMPUTE THE MATRIX E AND ITS DECOMPOSITION
!*** *** *** *** *** *** ***
FAC1=1.D0/(H*GAMMA)
IMPLCT_IF: IF (IMPLCT) THEN
   BANDED_IF:IF (BANDED) THEN
      ! --- THE MATRIX E (MAS IS A BANDED MATRIX, JACOBIAN A BANDED MATRIX)
      DO J=1,N
         I1J=MAX0(1,MUJAC+2-J)
         I2J=MIN(MBJAC,MUJAC+1-J+N)
         DO I=I1J,I2J
            E(I+MLE,J)=-FJAC(I,J)
         ENDDO
         I1B=MAX0(1,MUMAS+2-J)
         I2B=MIN0(MBB,MUMAS+1-J+N)
         DO I=I1B,I2B
            IB=I+MDIFF
            E(IB,J)=E(IB,J)+FAC1*FMAS(I,J)
         ENDDO
      ENDDO
      CALL DECB(N,LE,E,MLE,MUE,IP,IER)
      IF (IER /= 0) GOTO 78
   ELSE BANDED_IF
      IF (MLMAS /= N) THEN
      ! --- THE MATRIX E (MAS IS A BANDED MATRIX, JACOBIAN A FULL MATRIX)
         DO J=1,N
            DO I=1,N
               E(I,J)=-FJAC(I,J)
            ENDDO
         ENDDO
         DO J=1,N
            I1=MAX0(1,J-MUMAS)
            I2=MIN0(N,J+MLMAS)
            DO I=I1,I2
               E(I,J)=E(I,J)+FAC1*FMAS(I-J+MBDIAG,J)
            ENDDO
         ENDDO
         CALL DEC(N,LE,E,IP,IER)
         IF (IER /= 0) GOTO 78
      ELSE
         ! --- THE MATRIX E (MAS IS A FULL MATRIX, JACOBIAN A FULL MATRIX)
         IF (MLJAC == N) THEN
            DO J=1,N
               DO I=1,N
                  E(I,J)=FMAS(I,J)*FAC1-FJAC(I,J)
               ENDDO
            ENDDO
            CALL DEC(N,LE,E,IP,IER)
            IF (IER /= 0) GOTO 78
         ELSE
         ! --- THE MATRIX E (B IS A FULL MATRIX, JACOBIAN A BANDED MATRIX)
         END IF
      END IF
   END IF BANDED_IF
ELSE IMPLCT_IF
   IF (BANDED) THEN
      !--- THE MATRIX E (MAS=IDENTITY, JACOBIAN A BANDED MATRIX)
      DO J=1,N
         I1J=MAX0(1,MUJAC+2-J)
         I2J=MIN(MBJAC,MUJAC+1-J+N)
         DO I=I1J,I2J
            E(I+MLE,J)=-FJAC(I,J)
         ENDDO
         E(MDIAG,J)=E(MDIAG,J)+FAC1
      ENDDO
      CALL DECB(N,LE,E,MLE,MUE,IP,IER)
      IF (IER /= 0) GOTO 78
   ELSE
      !--- THE MATRIX E (MAS=IDENTITY, JACOBIAN A FULL MATRIX)
      IF (NHESS == 0) THEN
         DO J=1,N
            DO I=1,N
               E(I,J)=-FJAC(I,J)
               E(J,J)=E(J,J)+FAC1
            ENDDO
         ENDDO
         CALL DEC(N,LE,E,IP,IER)
         IF (IER /= 0) GOTO 79
      ELSE
         DO J=1,N-1
            J1=J+1
            E(J1,J)=-FJAC(J1,J)
         ENDDO
         DO J=1,N
            DO I=1,J
            E(I,J)=-FJAC(I,J)
            ENDDO
            E(J,J)=E(J,J)+FAC1
         ENDDO
         CALL DECH(N,LE,E,1,IP,IER)
         IF (IER /= 0) GOTO 79
      END IF
   END IF
END IF IMPLCT_IF
NDEC=NDEC+1
30 CONTINUE
IF (NSTEP > NMAX.OR.X+.1D0*H == X.OR.ABS(H) <= UROUND) GOTO 79
XPH=X+H
!--- LOOP FOR THE 5 STAGES
FACCO1=DMAX1(FACCO1,UROUND)**0.8D0
FACCO2=DMAX1(FACCO2,UROUND)**0.8D0
FACCO3=DMAX1(FACCO3,UROUND)**0.8D0
FACCO4=DMAX1(FACCO4,UROUND)**0.8D0
FACCO5=DMAX1(FACCO5,UROUND)**0.8D0
!*** *** *** *** *** *** ***
!STARTING VALUES FOR NEWTON ITERATION
!*** *** *** *** *** *** ***
LOOP59 DO ISTAGE=1,5
   SELECT CASE(ISTAGE)
      CASE(1)
         XCH=X+GAMMA*H
         IF (FIRST.OR.NEWTRE) THEN
            Z(1:N) = 0.D0
         ELSE
            S=1.D0+GAMMA*H/HOLD
            DO I=1,N
               Z(I)=S*(CONT(I+NN)+S*(CONT(I+NN2)+S*(CONT(I+NN3)+S*CONT(I+NN4))))-YHAT(I)
            ENDDO
         END IF
         G1(1:N) = 0.D0
         FACCON=FACCO1
      CASE(2)
         XCH=X+C2*H
         DO I=1,N
            Z1I=Z1(I)
            Z(I)=ETA1*Z1I
            G1(I)=ALPH21*Z1I
         ENDDO
         FACCON=FACCO2
      CASE(3)
         XCH=X+C3*H
         DO I=1,N
            Z1I=Z1(I)
            Z2I=Z2(I)
            Z(I)=ANU1*Z1I+ANU2*Z2I
            G1(I)=ALPH31*Z1I+ALPH32*Z2I
         ENDDO
         FACCON=FACCO3
      CASE(4)
         XCH=X+C4*H
         DO I=1,N
            Z1I=Z1(I)
            Z3I=Z3(I)
            Z(I)=AMU1*Z1I+AMU3*Z3I
            G1(I)=ALPH41*Z1I+ALPH42*Z2(I)+ALPH43*Z3I
         ENDDO
         FACCON=FACCO4
      CASE(5)
         XCH=XPH
         DO I=1,N
            Z1I=Z1(I)
            Z2I=Z2(I)
            Z3I=Z3(I)
            Z4I=Z4(I)
            Z(I)=E1*Z1I+E2*Z2I+E3*Z3I+E4*Z4I
            YHAT(I)=Z(I)
            G1(I)=ALPH51*Z1I+ALPH52*Z2I+ALPH53*Z3I+ALPH54*Z4I
          ENDDO
         FACCON=FACCO5
   END SELECT

   !*** *** *** *** *** *** ***
   !LOOP FOR THE SIMPLIFIED NEWTON ITERATION
   !*** *** *** *** *** *** ***
   NEWT=0
   THETA=ABS(THET)
   IF (REJECT) THETA=2*ABS(THET)
40 CONTINUE
   IF (NEWT >= NIT) THEN
      H=H/2.D0
      REJECT=.TRUE.
      NEWTRE=.TRUE.
      IF (CALJAC) GOTO 20
      GOTO 10
   END IF
! ---     COMPUTE THE RIGHT-HAND SIDE
   H1(1:N)=G1(1:N)-Z(1:N)
   CONT(1:N)=Y(1:N)+Z(1:N)
   CALL FCN(N,XCH,CONT,F1)
   NFCN=NFCN+1
! ---     SOLVE THE LINEAR SYSTEMS
   IF (IMPLCT) THEN
      IF (MLMAS /= N) THEN
         DO I=1,N
            S1=0.0D0
            J1B=MAX0(1,I-MLMAS)
            J2B=MIN0(N,I+MUMAS)
            DO J=J1B,J2B
               S1=S1+FMAS(I-J+MBDIAG,J)*H1(J)
            ENDDO
            F1(I)=S1*FAC1+F1(I)
         ENDDO
         IF (BANDED) THEN
          CALL SOLB(N,LE,E,MLE,MUE,F1,IP)
         ELSE
          CALL SOL(N,LE,E,F1,IP)
         END IF
      ELSE
         DO I=1,N
            S1=0.0D0
            DO J=1,N
               S1=S1+FMAS(I,J)*H1(J)
            ENDDO
            F1(I)=S1*FAC1+F1(I)
         ENDDO
         CALL SOL(N,LE,E,F1,IP)
      END IF
   ELSE
      F1(1:N)=H1(1:N)*FAC1+F1(1:N)
      IF (BANDED) THEN
         CALL SOLB(N,LE,E,MLE,MUE,F1,IP)
      ELSE
         IF (NHESS == 0) THEN
            CALL SOL(N,LE,E,F1,IP)
         ELSE
            DO MMM=N-2,1,-1
               MP=N-MMM
               MP1=MP-1
               I=IPHES(MP)
               IF (I /= MP) THEN
                  ZSAFE=F1(MP)
                  F1(MP)=F1(I)
                  F1(I)=ZSAFE
               ENDIF
               DO I=MP+1,N
                  F1(I)=F1(I)-FJAC(I,MP1)*F1(MP)
               ENDDO
            ENDDO
            CALL SOLH(N,LE,E,1,F1,IP)
            DO MMM=1,N-2
               MP=N-MMM
               MP1=MP-1
               DO I=MP+1,N
                  F1(I)=F1(I)+FJAC(I,MP1)*F1(MP)
               ENDDO
               I=IPHES(MP)
               IF (I == MP) CYCLE
               ZSAFE=F1(MP)
               F1(MP)=F1(I)
               F1(I)=ZSAFE
            ENDDO
         END IF
      END IF
   END IF
   NEWT=NEWT+1
   DYNO=0.D0
   DO I=1,N
      DENOM=SCAL(I)
      DYNO=DYNO+(F1(I)/DENOM)**2
   ENDDO
   DYNO=DSQRT(DYNO/N)
   ! ---     BAD CONVERGENCE OR NUMBER OF ITERATIONS TO LARGE
   IF (NEWT >= 2.AND.NEWT < NIT) THEN
      THETA=DYNO/DYNOLD
      IF (THETA < 0.99D0) THEN
         FACCON=THETA/(1.0D0-THETA)
         DYTH=FACCON*DYNO*THETA**(NIT-1-NEWT)
         QNEWT=DMAX1(1.0D-4,DMIN1(16.0D0,DYTH/FNEWT))
         IF (QNEWT >= 1.0D0) THEN
            H=.8D0*H*QNEWT**(-1.0D0/(NIT-NEWT))
            REJECT=.TRUE.
            NEWTRE=.TRUE.
            IF (CALJAC) GOTO 20
            GOTO 10
         END IF
      ELSE
         NEWTRE=.TRUE.
         GOTO 78
      END IF
   END IF
   DYNOLD=DYNO
   DO I=1,N
      Z(I)=Z(I)+F1(I)
   ENDDO
   NSOL=NSOL+1
   IF (FACCON*DYNO > FNEWT) GOTO 40
! --- END OF SIMPILFIED NEWTON
   SELECT CASE(ISTAGE)
      CASE(1)
         Z1(1:N) = Z(1:N)
         FACCO1=FACCON
      CASE(2)
         Z2(1:N) = Z(1:N)
         FACCO2=FACCON
      CASE(3)
         Z3(1:N) = Z(1:N)
         FACCO3=FACCON
      CASE(4)
         Z4(1:N) = Z(1:N)
         FACCO4=FACCON
      CASE(5)
         Z4(1:N) = Z(1:N)
         FACCO5=FACCON
   END SELECT
ENDDO LOOP59
!*** *** *** *** *** *** ***
!ERROR ESTIMATION
!*** *** *** *** *** *** ***
NSTEP=NSTEP+1
IF (IMPLCT) THEN
   H1(1:N) = FAC1*(Z5(1:N)-YHAT(1:N))
   IF (MLMAS == N) THEN
      DO I=1,N
         SUM=0.D0
         DO J=1,N
            SUM=SUM+FMAS(I,J)*H1(J)
            CONT(I)=SUM
         ENDDO
      ENDDO
   ELSE
      DO I=1,N
         SUM=0.D0
         J1B=MAX0(1,I-MLMAS)
         J2B=MIN0(N,I+MUMAS)
         DO J=J1B,J2B
            SUM=SUM+FMAS(I-J+MBDIAG,J)*H1(J)
            CONT(I)=SUM
         ENDDO
      ENDDO
   END IF
ELSE
   CONT(1:N)=FAC1*(Z5(1:N)-YHAT(1:N))
END IF
   IF (BANDED) THEN
      CALL SOLB(N,LE,E,MLE,MUE,CONT,IP)
   ELSE
   IF (NHESS == 0) THEN
      CALL SOL(N,LE,E,CONT,IP)
   ELSE
      DO MMM=N-2,1,-1
         MP=N-MMM
         MP1=MP-1
         I=IPHES(MP)
         IF (I /= MP) THEN
            ZSAFE=CONT(MP)
            CONT(MP)=CONT(I)
            CONT(I)=ZSAFE
         ENDIF
         CONT(MP+1:N)=CONT(MP+1:N)-FJAC(MP+1:N,MP1)*CONT(MP)
      ENDDO
      CALL SOLH(N,LE,E,1,CONT,IP)
      DO MMM=1,N-2
         MP=N-MMM
         MP1=MP-1
         CONT(MP+1:N)=CONT(MP+1:N)+FJAC(MP+1:N,MP1)*CONT(MP)
         I=IPHES(MP)
         IF (I == MP) CYCLE
         ZSAFE=CONT(MP)
         CONT(MP)=CONT(I)
         CONT(I)=ZSAFE
      ENDDO
   END IF
END IF
ERR=SUM((CONT(1:N)/SCAL(1:N))**2)
ERR=DMAX1(DSQRT(ERR/N),1.D-10)
! --- COMPUTATION OF HNEW
! --- WE REQUIRE .25<=HNEW/H<=4.
FAC=DMIN1(SAFE,CFAC/(NEWT+2*NIT))
QUOT=DMAX1(.25D0,DMIN1(4.D0,(ERR)**.25D0/FAC))
HNEW=H/QUOT
!*** *** *** *** *** *** ***
!IS THE ERROR SMALL ENOUGH ?
!*** *** *** *** *** *** ***
IF (ERR < 1.D0) THEN
   ! --- STEP IS ACCEPTED  
   FIRST=.FALSE.
   NACCPT=NACCPT+1
   HOLD=H
   XOLD=X
   ! --- COEFFICIENTS FOR CONTINUOUS SOLUTION
   DO I=1,N
      Z1I=Z1(I)
      Z2I=Z2(I)
      Z3I=Z3(I)
      Z4I=Z4(I)
      Z5I=Z5(I)
      CONT(I)=Y(I)
      Y(I)=Y(I)+Z5I
      CONT(I+NN)=D11*Z1I+D12*Z2I+D13*Z3I+D14*Z4I+D15*Z5I
      CONT(I+NN2)=D21*Z1I+D22*Z2I+D23*Z3I+D24*Z4I+D25*Z5I
      CONT(I+NN3)=D31*Z1I+D32*Z2I+D33*Z3I+D34*Z4I+D35*Z5I
      CONT(I+NN4)=D41*Z1I+D42*Z2I+D43*Z3I+D44*Z4I+D45*Z5I
      YHAT(I)=Z5I
      IF (ITOL == 0) THEN
         SCAL(I)=ATOL(1)+RTOL(1)*DABS(Y(I))
      ELSE
         SCAL(I)=ATOL(I)+RTOL(I)*DABS(Y(I))
      END IF
   ENDDO
   X=XPH
   IF (IOUT /= 0) THEN
      NRSOL=NACCPT+1
      XSOL=X
      XOSOL=XOLD
      H1(1:N) = Y(1:N)
      NSOLU=N
      HSOL=HOLD
      CALL SOLOUT(NRSOL,XOSOL,XSOL,H1,NSOLU,IRTRN)
      IF (IRTRN < 0) GOTO 79
   END IF
   CALJAC=.FALSE.
   IF ((X-XEND)*POSNEG+UROUND > 0.D0) THEN
      H=HOPT
      IDID=1
      RETURN
   END IF
   IF (IJAC == 0) CALL FCN(N,X,Y,Y0)
   NFCN=NFCN+1
   HNEW=POSNEG*DMIN1(DABS(HNEW),HMAX1)
   HOPT=HNEW
   IF (REJECT) HNEW=POSNEG*DMIN1(DABS(HNEW),DABS(H))
   REJECT=.FALSE.
   NEWTRE=.FALSE.
   IF ((X+HNEW/QUOT1-XEND)*POSNEG > 0.D0) THEN
      H=XEND-X
   ELSE
      QT=HNEW/H
      IF (THETA <= THET.AND.QT >= QUOT1.AND.QT <= QUOT2) GOTO 30
      H=HNEW
   END IF
   IF (THETA <= THET) GOTO 20
   GOTO 10
ELSE
   ! --- STEP IS REJECTED  
   REJECT=.TRUE.
   IF (FIRST) THEN
      H=H/10.D0
   ELSE
      H=HNEW
   END IF
   IF (NACCPT >= 1) NREJCT=NREJCT+1
   IF (CALJAC) GOTO 20
   GOTO 10
END IF
! --- UNEXPECTED STEP-REJECTION
78  CONTINUE
IF (IER /= 0) THEN
WRITE (6,*) ' MATRIX IS SINGULAR, IER=',IER,' X=',X,' H=',H
NSING=NSING+1
IF (NSING >= 6) GOTO 79
END IF
H=H*0.5D0
REJECT=.TRUE.
IF (CALJAC) GOTO 20
GOTO 10
! --- FAIL EXIT
79  WRITE (6,979) X,H,IER
979  FORMAT(' EXIT OF SDIRK4 AT X=',D14.7,'   H=',D14.7,'   IER=',I4)
IDID=-1
RETURN
END
!
REAL*8 FUNCTION CONTS4(I,X)
!----------------------------------------------------------
!THIS FUNCTION CAN BE USED FOR CONINUOUS OUTPUT FOR THE
!SUBROUTINE SDIRK4. IT PROVIDES AN APPROXIMATION
!TO THE I-TH COMPONENT OF THE SOLUTION AT X.
!----------------------------------------------------------
IMPLICIT REAL*8 (A-H,O-Z)
COMMON /CONT/NN,NN2,NN3,NN4,XOLD,HSOL,CONT(1)
S=(X-XOLD)/HSOL
CONTS4=CONT(I)+S*(CONT(I+NN)+S*(CONT(I+NN2)+S*(CONT(I+NN3)+S*CONT(I+NN4))))
RETURN
END

END MODULE DIRK